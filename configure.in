#/*****************************************************************************
#**
#** $Id: configure.in,v 1.1 2001/03/14 02:31:10 saturn_de Exp $
#**
#**	This program is free software; you can redistribute it and/or
#**	modify it under the terms of the GNU General Public License
#**	as published by the Free Software Foundation; either version
#**	2 of the License, or (at your option) any later version.
#**
#** (C) 1999,2000 Harald Hoyer <DLDialog@parzelle.de> 
#**
#*****************************************************************************/
dnl We need autoconf --version >= 2.1
AC_PREREQ(2.13)

dnl Process this file with autoconf to produce a configure script.
AC_INIT(./acinclude.m4)


dnl dld_gui.h Version see README.developer
dnl Increment CURRENT if any interfaces have been added, removed,
dnl or changed since the last update and set REVISION to 0.
CURRENT=26

dnl Increment this every time the code changes
REVISION=0

dnl If any interfaces have been added since the last public release, 
dnl then increment AGE.
dnl If any interfaces have been removed since the last public release,
dnl then set AGE to 0.
AGE=0

PROG_MAJOR=1
PROG_MINOR=$[[$CURRENT-22]].$[[$REVISION-1+2]]

PROG_VERSION=$PROG_MAJOR.$PROG_MINOR

AM_INIT_AUTOMAKE(dldialog, $PROG_VERSION)

AM_CONFIG_HEADER(config.h)

GUI_VERSION_INFO=$CURRENT:$REVISION:$AGE
AC_SUBST(GUI_VERSION_INFO)

dnl Checks for programs.
AC_PROG_CC
AC_PROG_MAKE_SET
AC_PROG_CXX
AM_PROG_LEX
AC_PROG_YACC

dnl Checks for dynamic libraries.
AM_DISABLE_STATIC
AM_ENABLE_SHARED
AM_PROG_LIBTOOL

ALL_LINGUAS="de"
AM_GNU_GETTEXT

dnl Checks for libraries.

AC_ARG_ENABLE(debug,
[  --enable-debug          debug [default=>>no]],
[
case "$enableval" in
yes) DEBUG="-DDEBUG -g" ;;
*)
  DEBUG="";;
esac],
DEBUG="")

AC_LANG_CPLUSPLUS dnl switch to C++

dnl Checks for header files.
AC_HEADER_STDC

dnl Checks for typedefs, structures, and compiler characteristics.
dnl AC_C_CONST Produces error with linux egcs

dnl Check for QT Libraries
AC_ARG_WITH(qt,
    [  --without-qt            build dldialog without qt support. ],
    [  
case "$withval" in
yes) AC_PATH_QT;;
no)  AC_MSG_WARN([ dldialog will be build without qt support! ]);;
*);;
esac ], [ AC_PATH_QT ])
AM_CONDITIONAL(BUILDQT, test "$with_qt" != "no")

dnl Check for TV Libraries
AC_ARG_WITH(tv,
    [  --without-tv            build dldialog without tvision support. ],
    [ 
case "$withval" in
yes) AC_PATH_TV;;
no)  AC_MSG_WARN([ dldialog will be build without tvision support! ]);;
*);;
esac ], [AC_PATH_TV ])


AM_CONDITIONAL(BUILDTV, test "$with_tv" != "no")

AC_CHECK_PROG(KDOC, kdoc, kdoc)
AC_CHECK_PROG(SGML2HTML, sgml2html, sgml2html)
AC_CHECK_PROG(LATEX, latex, latex)

AM_CONDITIONAL(CLASSDOC, test -n "$KDOC")
AM_CONDITIONAL(SGMLTOOLS, test -n "$SGML2HTML")
AM_CONDITIONAL(HAVE_LATEX, test -n "$LATEX")

CXXFLAGS="$CXXFLAGS $DEBUG"

AC_ARG_ENABLE(final,
 [  --enable-final          build size optimized lib (needs lots of memory)],
 use_final=$enableval, use_final=no)

AC_SUBST(HAVE_FINAL)
HAVE_FINAL=$use_final


AC_OUTPUT(Makefile \
   dldialog.spec \
	tests/Makefile \
	doc/Makefile \
	doc/classdoc/Makefile \
	doc/classdoc/qt/Makefile \
   doc/classdoc/tv/Makefile \
   src/Makefile \
	src/DLD_GEN/Makefile \
   src/DLD_TV/Makefile \
	src/DLD_QT/Makefile \
	perl/Makefile \
	perl/doc/Makefile \
	intl/Makefile \
	po/Makefile.in \
)

